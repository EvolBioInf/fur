#!/usr/bin/awk -f
BEGIN {
  FS = "\t"
  defMaxMism = 5
  defMaxLen = 4000
  defNumThreads = 1
  defEvalue = 10
  if (!query || !db || !(taxid || negativeTaxid)) {
    print  "checkPrim: Check the specificity of PCR primers using BLAST"
    print  "Usage: awk -f checkPrim "
    print  "-v query=<query>"
    print  "-v db=<db>"
    print  "-v taxid=<taxid> || -v negativeTaxid=<negativeTaxid>"
    printf "[-v maxMism=<maxMism>; default: %d]\n", defMaxMism
    printf "[-v maxLen=<maxLen>; default: %d]\n", defMaxLen
    printf "[-v numThreads=<numThreads>; default: %d]\n", defNumThreads
    printf "[-v evalue=<evalue>; default: %d]\n", defEvalue
    exit 0
  }
  if (!maxMism)
    maxMism = defMaxMism
  if (!maxLen)
    maxLen = defMaxLen
  if (!numThreads)
    numThreads = defNumThreads
  if (!evalue)
    evalue = defEvalue
  tmpl = "blastn -task blastn-short -query %s -db %s -outfmt "
  tmpl = tmpl "\"6 qacc sacc mismatch sstart send staxid ssciname "
  tmpl = tmpl "qlen length\" -num_threads %d -evalue %d "
  if (taxid)
    tmpl = tmpl "| awk '$6 == \"%s\"' "
  else {
    taxid = negativeTaxid
    tmpl = tmpl "| awk '$6 != \"%s\"' "
  }
  tmpl = tmpl "| awk '$3 <= %d && $8 == $9'"
  tmpl = tmpl "| sort -k 2,2 -k 4,4n"
  cmd = sprintf(tmpl, query, db, numThreads, evalue, taxid, maxMism)
  n = 1 - 1
  while (cmd | getline) {
    qacc[n] = $1
    sacc[n] = $2
    staxid[n] = $6
    ssciname[n] = $7
    if ($4 < $5) {
      sstart[n] = $4
      send[n] = $5
      strand[n] = 0
    } else {
      sstart[n] = $5
      send[n] = $4
      strand[n] = 1
    }
    n++
  }
  close(cmd)
  printf "# qacc\tqacc\tsacc\tsstart\tsend\tstaxid\t"
  print  "ssciname"
  for (i = 0; i < n - 1; i++) {
    j = i + 1
    l = send[j] - sstart[i] - 1
    while (sacc[i] == sacc[j] && j < n && l <= maxLen) {
      if (strand[i] == 0 && strand[j] == 1)
          printf("%s\t%s\t%s\t%d\t%d\t%d\t%s\n",
                   qacc[i], qacc[j], sacc[i], sstart[i], send[j], staxid[i],
                   ssciname[i])
      j++
    }
  }
}
